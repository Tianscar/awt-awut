plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
}

ext {
    libraryName = 'Abstract Window Utilities Toolkit'
    libraryGroupName = 'com.tianscar.awt'
    libraryVendorName = 'Tianscar'

    libraryVersionCode = 1
    libraryVersionName = '0.1.0'

    librarySourceCompatibility = JavaVersion.VERSION_1_8
    libraryTargetCompatibility = JavaVersion.VERSION_1_8
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
sourceCompatibility = librarySourceCompatibility
targetCompatibility = libraryTargetCompatibility

group = libraryGroupName
version = libraryVersionName

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.github.jnr:jnr-ffi:2.2.13'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = libraryGroupName
            artifactId = rootProject.name
            version = libraryVersionName
            from components.java
            pom {
                name = libraryName
                description = 'Utilities and bug fixes for Java Abstract Window Toolkit (AWT)'
                url = 'https://github.com/Tianscar/awt-awut'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://spdx.org/licenses/MIT.html'
                    }
                }
                developers {
                    developer {
                        id = 'Tianscar'
                        name = 'Karstian Lee'
                        email = 'tianscar@protonmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com/Tianscar/awt-awut.git'
                    developerConnection = 'scm:git:git@github.com/Tianscar/awt-awut.git'
                    url = 'https://github.com/Tianscar/awt-awut'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            if (project.version.toString().endsWith("-SNAPSHOT")) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
            credentials {
                username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

signing {
    sign(publishing.publications.mavenJava)
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
